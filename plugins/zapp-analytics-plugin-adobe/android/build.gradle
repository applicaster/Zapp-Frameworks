import org.gradle.util.VersionNumber

apply plugin: 'com.android.library'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 28
        consumerProguardFiles 'proguard-rules.pro'
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'jsr305_annotations/Jsr305_annotations.gwt.xml'

    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }
    }
}

repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://maven.google.com' }
    maven { // Applicaster
        credentials {
            username System.getenv("MAVEN_USERNAME")
            password System.getenv("MAVEN_PASSWORD")
        }
        url 'https://dl.bintray.com/applicaster-ltd/maven'
    }
    google()
}

def safeExtGet(prop, fallback) {
    def ver = rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
    if (VersionNumber.parse(fallback) > VersionNumber.parse(ver)) {
        throw new Exception("Version mismatch for $prop: $ver found, at least $fallback required")
    }
    return ver
}

dependencies {
    // Applicaster SDK
    if (!findProject(':applicaster-android-sdk') || project.hasProperty('forceClosedDependencies')) {
        def sdkVersion = safeExtGet('applicaster_android_sdk_core_version', '8.0.0')
        api ("com.applicaster:applicaster-android-sdk-core:${sdkVersion}")
    }
    else {
        api project(':applicaster-android-sdk')
    }

    implementation 'com.adobe.marketing.mobile:analytics:1.2.6'
    implementation 'com.adobe.marketing.mobile:userprofile:1.1.0'
    implementation 'com.adobe.marketing.mobile:sdk-core:1.6.0'
}
